# Options
set (GAOL_DIR "" CACHE PATH "Path to the Gaol lib and include directories")

# Looking for gaol.h
set (GAOL_HDR "gaol/gaol.h")
set (MSG "Looking for ${GAOL_HDR}")
message (STATUS "${MSG}")
find_path (gaol_INCLUDE_DIR ${GAOL_HDR} HINTS ${GAOL_DIR} PATH_SUFFIXES include
           DOC "Set to exact include directory to bypass internal test")
if (gaol_INCLUDE_DIR)
  message (STATUS "${MSG} -- found at ${gaol_INCLUDE_DIR}")
else()
  message (FATAL_ERROR "${MSG} -- not found")
endif()

# Looking for libgaol
foreach (LIB gdtoa ultim gaol)
  set (MSG "Looking for ${LIB}")
  message (STATUS "${MSG}")
  find_library (${LIB}_LIBRARY NAMES ${LIB} HINTS ${GAOL_DIR}
                PATH_SUFFIXES lib lib/${LIB} ${LIB} ${LIB}/lib
                DOC "Set to exact lib directory to bypass internal test")
  if (${LIB}_LIBRARY)
    message (STATUS "${MSG} -- found at ${${LIB}_LIBRARY}")
  else()
    message (FATAL_ERROR "${MSG} -- not found")
  endif()
endforeach ()
# 
mark_as_advanced (gaol_INCLUDE_DIR gdtoa_LIBRARY ultim_LIBRARY gaol_LIBRARY)

# Set INTERVAL_LIB_* variables
set (INTERVAL_LIB_FOUND ${GAOL_FOUND} PARENT_SCOPE)
set (INTERVAL_LIB_INCLUDE_DIRS ${gaol_INCLUDE_DIR} PARENT_SCOPE)
set (INTERVAL_LIB_LIBRARIES ${gdtoa_LIBRARY} ${ultim_LIBRARY} ${gaol_LIBRARY} PARENT_SCOPE)
set (INTERVAL_LIB_DEFINITIONS "" PARENT_SCOPE)

# With Gaol, we add "-Wno-deprecated" to remove lots of warnings generated by
# a function definition in gaol_interval.h
CHECK_CXX_COMPILER_FLAG ("-Wno-deprecated" COMPILER_SUPPORTS_WNO_DEPRECATED)
if (COMPILER_SUPPORTS_WNO_DEPRECATED)
  set (INTERVAL_LIB_CXXFLAGS "-Wno-deprecated" PARENT_SCOPE)
else ()
  set (INTERVAL_LIB_CXXFLAGS "" PARENT_SCOPE)
endif()
