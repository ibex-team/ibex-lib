%{
//============================================================================
//                                  I B E X                                   
// File        : Yacc/Bison input for Ibex parser
// Author      : Gilles Chabert
// Copyright   : IMT Atlantique (France)
// License     : See the LICENSE file
// Created     : Jun 12, 2012
// Last Update : Nov 03, 2019
//===========================================================================

#include "ibex_P_Struct.h"

using namespace ibex;
using namespace parser;
using namespace std;

%}	

%union{
  char*     str;
  int       itg;
  double    real;
  
  ibex::Interval* itv;
  
  ibex::Dim*      dim;
  
  ibex::parser::P_NumConstraint*                constraint;
  std::vector<ibex::parser::P_NumConstraint*>*  constraints;

  const ibex::parser::P_ExprNode*               expression;
  std::vector<const ibex::parser::P_ExprNode*>* expressions;

}

%token TK_CHOCO                        // pseudo-start token

%token <str> TK_CONSTANT
%token <str> TK_NEW_SYMBOL
%token <str> TK_FUNC_SYMBOL
%token <str> TK_EXPR_TMP_SYMBOL
%token <str> TK_ENTITY
%token <str> TK_ITERATOR
%token <str> TK_UNARY_OP               // generic (user-defined) unary operator
%token <str> TK_BINARY_OP              // generic (user-defined) binary operator

%token <str> TK_STRING
%token <itg> TK_INT_CST
%token <real> TK_FLOAT
%token TK_PI TK_INFINITY
%token <_bool> TK_BOOL

%token TK_PARAM TK_CONST TK_VARS TK_FUNCTION 
%token TK_DIFF 
%token TK_MIN TK_MAX TK_INF TK_MID TK_SUP TK_SIGN TK_ABS TK_SUM
%token TK_SQRT TK_SQR TK_POW TK_EXPO  TK_LOG 
%token TK_COS  TK_SIN   TK_TAN  TK_ACOS  TK_ASIN  TK_ATAN TK_ATAN2
%token TK_COSH TK_SINH  TK_TANH TK_ACOSH TK_ASINH TK_ATANH TK_FLOOR TK_CEIL TK_INTEGER
%token TK_LEQ  TK_GEQ   TK_EQU  TK_ASSIGN TK_CHI
%token TK_BEGIN TK_END TK_FOR TK_FROM TK_TO TK_RETURN
%token TK_CTRS TK_MINIMIZE

%token TK_IN

%nonassoc TK_EQU '<' TK_LEQ '>' TK_GEQ    /* we forbid a < b < c */
%left '+' '-' TK_UNION
%left '*' '/' TK_INTERSEC
%left '^' '\''
%left '[' '('

/* --------------------------- Nonterminals types -------------------------- */

%type<dim>        dimension

/* constraints */
%type<constraints>  ctr_blk_list 
%type<constraints>  ctr_blk_list1 // ctr list ended by a constraint (must be followed by semicolon)
%type<constraints>  ctr_blk_list2 // ctr list ended by a loop (semicolon is optional)
%type<constraint>   ctr_loop
%type<constraint>   ctr 
 
%type<itv>         interval

/* expressions */
%type<expression>  expr
%type<expression>  expr_index
%type<expressions> expr_row
%type<expressions> expr_col
%%


program       :                                 { pstruct->begin(); }
                decl_opt_cst 
                decl_opt_fncs
                system_or_func                  { pstruct->end(); }
              |                                 { pstruct->begin(); 
                                                  pstruct->scope().push(); /* new scope for temporary symbols */} 
                choco_ctr                       { pstruct->scope().pop();
                                                  pstruct->end(); }
              ; 

system_or_func: TK_VARS decl_var_list ';'   
                decl_opt_fncs
                decl_opt_goal
                decl_opt_ctrs                   
              |
              ;
              
choco_ctr     : ctr_blk_list                    { pstruct->source.ctrs=new P_ConstraintList($1); }
              ;

/**********************************************************************************************************************/
/*                                                SYMBOLS                                                         */
/**********************************************************************************************************************/

decl_opt_cst  :  
			  | TK_CONST
              | TK_CONST decl_cst_list ';'
	          ;

decl_cst_list : decl_cst                                    
              | decl_cst_list ';' decl_cst
              ;

decl_cst      : TK_NEW_SYMBOL dimension 
                equ_or_in expr                  { Domain* d=new Domain(*$2);
                                                  init_symbol_domain($1, *d, $4->_2domain());
                                                  pstruct->scope().add_cst($1, *d, false); 
                                                  delete d;
                                                  free($1); delete $2; delete $4; }
              | '*' TK_NEW_SYMBOL dimension 
                equ_or_in expr                  { Domain* d=new Domain(*$3);
                                                  init_symbol_domain($2, *d, $5->_2domain());
                                                  pstruct->source.mutable_constants.insert_new($2,d);
                                                  pstruct->scope().add_cst($2, *d, true); 
                                                  // note: d will be deleted by ~System().
                                                  free($2); delete $3; delete $5; }
              ;
              
equ_or_in     : TK_EQU | TK_IN ;
 
decl_var_list : decl_var                             
              | decl_var_list ';' decl_var           
              | decl_var_list ',' decl_var           
              ;
  
decl_var      : TK_NEW_SYMBOL dimension         { pstruct->scope().add_var($1,$2);  
		                                          free($1); delete $2; }
              | TK_NEW_SYMBOL dimension 
	            TK_IN expr                      { pstruct->scope().add_var($1,$2,$4->_2domain()); 
						                          free($1); delete $2; delete $4; }
              ; 

dimension     :                                 { $$=new Dim(); }
              | '[' expr ']'                    { $$=new Dim(Dim::col_vec($2->_2int())); 
                                                  delete $2;  }
              | '[' expr ']' '[' expr ']'       { $$=new Dim(Dim::matrix($2->_2int(),$5->_2int())); 
                                                  delete $2; delete $5; }
	          ;

interval      : '[' expr ',' expr ']'           { $$=new Interval($2->_2dbl(true), $4->_2dbl(false)); 
                                                  delete $2; delete $4; }
              ;

/**********************************************************************************************************************/
/*                                                FUNCTIONS                                                           */
/**********************************************************************************************************************/

decl_opt_fncs : decl_opt_fncs function 
              | 
              ;

function      : TK_FUNCTION                     { pstruct->begin_function(); }
                TK_NEW_SYMBOL
                '(' fnc_inpt_list ')'
                fnc_code
                TK_RETURN expr semicolon_opt
                TK_END                          { 
                                                  pstruct->end_function($3,*$9);        
                                                  free($3); delete $9;
                                                 }
              ;

semicolon_opt : ';' | ;

fnc_inpt_list : fnc_inpt_list ',' fnc_input     
              | fnc_input                       
              ;

fnc_input     : TK_NEW_SYMBOL dimension         { pstruct->scope().add_var($1,$2);
                                                  free($1); delete $2; }
              ;

fnc_code      : fnc_code fnc_assign ';'              
              |                       
              ;

fnc_assign    : TK_NEW_SYMBOL TK_EQU expr       { /* TODO: if this tmp symbol is not used, the generated expr will never be deleted */
                                                  pstruct->scope().add_expr_tmp_symbol($1,&$3->generate()); free($1); delete $3; }
              | TK_CONSTANT TK_EQU expr         { cerr << "Warning: line " << ibex_lineno << ", local variable " << $1 << " shadows the constant of the same name\n"; 
                                                  pstruct->scope().add_expr_tmp_symbol($1,&$3->generate()); free($1); delete $3; } 
              ;

/**********************************************************************************************************************/
/*                                                  GOAL                                                              */
/**********************************************************************************************************************/
decl_opt_goal :                                 { pstruct->source.goal = NULL; }
              | TK_MINIMIZE expr semicolon_opt  { pstruct->source.goal = $2; }
              ;

/**********************************************************************************************************************/
/*                                                CONSTRAINTS                                                         */
/**********************************************************************************************************************/

decl_opt_ctrs : 
              | TK_CTRS                         { pstruct->scope().push(); /* new scope for temporary symbols */ } 
                ctr_blk_list TK_END             { pstruct->source.ctrs=new P_ConstraintList($3);
                                                  pstruct->scope().pop(); }
              | TK_CTRS semicolon_opt TK_END    { pstruct->source.ctrs=new P_ConstraintList(new vector<P_NumConstraint*>()); }
	          ;
	          
ctr_blk_list  : ctr_blk_list1 semicolon_opt     { $$ = $1; }
              | ctr_blk_list2 semicolon_opt     { $$ = $1; }
      		  ;

ctr_blk_list1 : ctr_blk_list1 ';' ctr           { $1->push_back($3); $$ = $1; }
			  | ctr_blk_list2 semicolon_opt ctr { $1->push_back($3); $$ = $1; }
              | ctr                             { $$ = new vector<P_NumConstraint*>();
                                                  $$->push_back($1); }
      		  ;
      		  
ctr_blk_list2 : ctr_blk_list1 ';' ctr_loop      { $1->push_back($3); $$ = $1; }
			  | ctr_blk_list2 semicolon_opt ctr_loop 
			                                    { $1->push_back($3); $$ = $1; }
              | ctr_loop                        { $$ = new vector<P_NumConstraint*>();
                                                  $$->push_back($1); }
      		  ;

ctr_loop      : TK_FOR TK_NEW_SYMBOL TK_EQU
                expr ':' expr ';'               { pstruct->scope().push();
                                                  pstruct->scope().add_iterator($2); }
                ctr_blk_list 
                TK_END                          { $$ = new P_ConstraintLoop($2, $4, $6, $9); 
                                                  pstruct->scope().pop();
                                                  free($2); }
              ;

ctr           : expr TK_EQU expr                { $$ = new P_OneConstraint($1,EQ,$3); }
              | expr TK_LEQ expr                { $$ = new P_OneConstraint($1,LEQ,$3); }
              | expr TK_GEQ expr                { $$ = new P_OneConstraint($1,GEQ,$3); }
              | expr   '<'  expr                { $$ = new P_OneConstraint($1,LT,$3); }
              | expr   '>'  expr                { $$ = new P_OneConstraint($1,GT,$3); }
              | expr TK_IN  expr                { $$ = new P_ThickEquality($1,$3->_2itv()); delete $3; }
              | TK_INTEGER  '(' expr ')'        { $$ = new P_OneConstraint(saw($3),EQ,NULL); }
              | '(' ctr ')'                     { $$ = $2; }
              | TK_NEW_SYMBOL TK_EQU expr       { $$ = new P_TmpSymbolDecl($1,$3); 
                                                  pstruct->scope().add_expr_tmp_symbol($1); free($1); }
              ; 
              
/**********************************************************************************************************************/
/*                                                EXPRESSIONS                                                         */
/**********************************************************************************************************************/

expr          : expr '+' expr	                { $$ = *$1 + *$3;     }
              | expr '*' expr	                { $$ = *$1 * *$3;     }
              | expr '-' expr	                { $$ = *$1 - *$3;     }
              | expr '/' expr	                { $$ = *$1 / *$3;     }
              | TK_MAX '(' expr_row ')'         { $$ = max($3); delete $3; }
              | TK_MIN '(' expr_row ')'         { $$ = min($3); delete $3; }
              | TK_SUM '(' TK_NEW_SYMBOL TK_EQU 
                           expr ':' expr ','    { pstruct->scope().push();
                                                  pstruct->scope().add_iterator($3); }
                expr ')'                        { $$ = sum($10, $3, $5, $7);
                                                  pstruct->scope().pop();
                                                  free($3); }
              | TK_ATAN2 '(' expr ',' expr ')'  { $$ = atan2($3,$5);  }
              | '-' expr                        { $$ = -*$2;          }
              | TK_ABS  '(' expr ')'            { $$ = abs  ($3);     }
              | TK_SIGN '(' expr ')'            { $$ = sign ($3);     }
              | expr '\''	                    { $$ = transpose($1); }
              | TK_SQRT '(' expr ')'            { $$ = sqrt ($3);     }
              | TK_SQR '(' expr ')'             { $$ = sqr ($3);      }
              | TK_POW '(' expr ',' expr ')'    { $$ = pow($3, $5);   }
              | TK_EXPO '(' expr ')'            { $$ = exp  ($3);     }
              | TK_LOG '(' expr ')'             { $$ = log  ($3);     }
              | TK_COS '(' expr ')'             { $$ = cos  ($3);     }
              | TK_SIN '(' expr ')'             { $$ = sin  ($3);     }
              | TK_TAN '(' expr ')'             { $$ = tan  ($3);     }
              | TK_ACOS '(' expr ')'            { $$ = acos ($3);     }
              | TK_ASIN '(' expr ')'            { $$ = asin ($3);     }
              | TK_ATAN '(' expr ')'            { $$ = atan ($3);     }
              | TK_COSH '(' expr ')'            { $$ = cosh ($3);     }
              | TK_SINH '(' expr ')'            { $$ = sinh ($3);     }
              | TK_TANH '(' expr ')'            { $$ = tanh ($3);     }
              | TK_ACOSH '(' expr ')'           { $$ = acosh($3);     }
              | TK_ASINH '(' expr ')'           { $$ = asinh($3);     }
              | TK_ATANH '(' expr ')'           { $$ = atanh($3);     }
              | TK_FLOOR '(' expr ')'           { $$ = floor($3);     }
              | TK_CEIL  '(' expr ')'           { $$ = ceil($3);      }
              | TK_CHI '(' expr ',' expr ',' expr ')'  
                                                { $$ = chi($3,$5,$7); }
              | '+' expr                        { $$ = $2; }
              | '(' expr ')'		            { $$ = $2; }
              | '<' expr ',' expr '>'           { $$ = new P_ExprConstant(ball($2->_2domain(),$4->_2dbl(false))); }
              | expr '^' expr	                { $$ = pow($1, $3); }
              | expr '[' expr_index ']'         { $$ = expr_with_index($1,$3,false);  }
              | expr '(' expr_index ')'         { $$ = expr_with_index($1,$3,true); }
              | expr '(' expr_index ',' expr_index ')'         
                                                { $$ = expr_with_index($1,$3,$5,true); }
              | '(' expr_row ')'                { $$ = row_vec($2); delete $2; }
              | '(' expr_col ')'                { $$ = col_vec($2); delete $2; }
              | TK_ENTITY                       { $$ = new P_ExprVarSymbol($1); free($1); /* cannot happen inside a function expr */}
              | TK_ITERATOR                     { $$ = new P_ExprIter($1); free($1); }
              | TK_EXPR_TMP_SYMBOL              { $$ = new P_ExprTmpSymbol($1); free($1); /* not this (we do **not** build DAG with P_ExprNodes!) ---> &pstruct->scope().get_tmp_expr_node($1); */ }
              | TK_CONSTANT                     { $$ = new P_ExprCstSymbol($1); free($1); }
              | TK_FUNC_SYMBOL '(' expr ')'     { $$ = apply(pstruct->scope().get_func($1), *$3); free($1); }
              | TK_FUNC_SYMBOL '(' expr_row ')' { $$ = apply(pstruct->scope().get_func($1), *$3); free($1); delete $3; }
//              | TK_NEW_SYMBOL                   { ibexerror("unknown symbol"); }
              | TK_FLOAT                        { $$ = new P_ExprConstant($1); }
              | TK_PI                           { $$ = new P_ExprConstant(Interval::pi()); }
              | TK_INFINITY                     { $$ = dbl_infinity(); }              
              | TK_INT_CST                      { $$ = new P_ExprConstant($1); }
              | interval                        { $$ = new P_ExprConstant(*$1); delete $1; }
              | TK_INF '(' expr ')'             { $$ = inf($3); }
              | TK_MID '(' expr ')'             { $$ = mid($3); }
              | TK_SUP '(' expr ')'             { $$ = sup($3); }
              | TK_DIFF '(' expr_row ')'        { $$ = diff($3); delete $3; }
              | TK_UNARY_OP '(' expr ')'        { $$ = new P_ExprGenericUnaryOp($1,*$3); free($1); }
              | TK_BINARY_OP '(' expr ',' expr ')'      
                                                { $$ = new P_ExprGenericBinaryOp($1,*$3,*$5); free($1); }
              ;
	      
expr_row      : expr_row  ',' expr              { $1->push_back($3); $$=$1; }
              | expr ',' expr                   { $$ = new vector<const P_ExprNode*>(); 
                                                  $$->push_back($1); $$->push_back($3); }
              ;

expr_col      : expr_col  ';' expr              { $1->push_back($3); $$=$1; }
              | expr ';' expr                   { $$ = new vector<const P_ExprNode*>(); 
                                                  $$->push_back($1); $$->push_back($3); }
              ; 
              
expr_index    : ':'                             { $$ = index_all(); }
	          | expr                            { $$ = index($1); }
              | expr ':' expr                   { $$ = index_range($1,$3); }
              ;
	          	
     
                           
